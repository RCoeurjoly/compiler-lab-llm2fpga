{ pkgs, mlir, circt, yosysPkg, yosysSlang, torchMlir, python, pipelineScripts
, directLowerScript }:
let
  stageNames = [
    "torch"
    "torch-stats"
    "linalg"
    "cf"
    "cf-stats"
    "handshake"
    "hs-ext"
    "hw0"
    "hw"
    "hw-clean"
    "sv-mlir"
    "sv"
    "direct-fail-fast"
    "direct-sv-smoke"
    "direct-sv-smoke-hw-clean"
    "direct-sv-lint"
    "direct-sv-lint-hw-clean"
    "direct-functional"
    "il"
    "yosys-stat"
  ];

  torchMlirOpt = let
    candidates = [
      "${torchMlir}/bin/torch-mlir-opt"
      "${torchMlir}/${python.sitePackages}/torch_mlir/_mlir_libs/torch-mlir-opt"
      "${torchMlir}/${python.sitePackages}/torch_mlir/torch_mlir/_mlir_libs/torch-mlir-opt"
    ];
    matches = builtins.filter builtins.pathExists candidates;
  in if matches == [ ] then
    throw "Unable to locate torch-mlir-opt in ${torchMlir}"
  else
    builtins.head matches;

  mkTorchInput = { name, torchMlirInput ? null, torchInputCommand ? null
    , torchInputBuildInputs ? [ ] }:
    if torchMlirInput != null then
      torchMlirInput
    else if torchInputCommand != null then
      pkgs.runCommand "${name}-torch-input.mlir" {
        buildInputs = torchInputBuildInputs;
      } ''
        set -euo pipefail
        ${torchInputCommand}
      ''
    else
      throw
      "registerModel(${name}): provide torchMlirInput or torchInputCommand";

  mkTorchDerivation = { name, torchMlirInput }:
    pkgs.runCommand "${name}-torch.mlir" { } ''
      cp ${torchMlirInput} "$out"
    '';

  mkMlirOpStatsDerivation = { name, stageName, tool, input }:
    pkgs.runCommand "${name}-${stageName}.stats" { } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/mlir_op_stats.sh \
        ${tool} ${input} "$out"
    '';

  mkLinalgDerivation = { name, torch }:
    pkgs.runCommand "${name}-linalg.mlir" { buildInputs = [ torchMlir ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/torch_to_linalg.sh \
        ${torchMlirOpt} ${torch} "$out"
    '';

  mkCfDerivation = { name, linalg }:
    pkgs.runCommand "${name}-cf.mlir" { buildInputs = [ mlir ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/linalg_to_cf.sh \
        ${mlir}/bin/mlir-opt ${linalg} "$out"
    '';

  mkHandshakeDerivation = { name, cf }:
    pkgs.runCommand "${name}-handshake.mlir" {
      buildInputs = [ mlir circt ];
    } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/cf_to_handshake.sh \
        ${circt}/bin/circt-opt ${mlir}/bin/mlir-opt ${cf} "$out"
    '';

  mkHandshakeLsqDerivation = { name, cf }:
    pkgs.runCommand "${name}-handshake.mlir" {
      buildInputs = [ mlir circt ];
    } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/cf_to_handshake_lsq.sh \
        ${circt}/bin/circt-opt ${mlir}/bin/mlir-opt ${cf} "$out"
    '';

  mkHsExtDerivation = { name, handshake }:
    pkgs.runCommand "${name}-hs-ext.mlir" { buildInputs = [ circt ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/handshake_to_hs_ext.sh \
        ${circt}/bin/circt-opt ${handshake} "$out"
    '';

  mkHw0Derivation = { name, hsExt }:
    pkgs.runCommand "${name}-hw0.mlir" { buildInputs = [ circt ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/hs_ext_to_hw0.sh \
        ${circt}/bin/circt-opt ${hsExt} "$out"
    '';

  mkHwDerivation = { name, hw0 }:
    pkgs.runCommand "${name}-hw.mlir" { buildInputs = [ circt ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/hw0_to_hw.sh \
        ${circt}/bin/circt-opt ${hw0} "$out"
    '';

  mkHwCleanDerivation = { name, hw }:
    pkgs.runCommand "${name}-hw-clean.mlir" { buildInputs = [ circt ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/hw_to_hw_clean.sh \
        ${circt}/bin/circt-opt ${hw} "$out"
    '';

  mkSvMlirDerivation = { name, hwClean }:
    pkgs.runCommand "${name}-sv-mlir" { buildInputs = [ circt ]; } ''
      ${pkgs.bash}/bin/bash ${pipelineScripts}/hw_clean_to_sv_mlir.sh \
        ${circt}/bin/circt-opt ${hwClean} "$out"
    '';

  mkSvDerivation = { name, svMlir, allowHwExterns ? false, fpPrimsSv ? null }:
    pkgs.runCommand "${name}-sv" { buildInputs = [ circt python ]; } ''
      ${pkgs.lib.optionalString allowHwExterns ''
        export ALLOW_HW_EXTERNS=1
      ''}
      ${pkgs.lib.optionalString (fpPrimsSv != null) ''
        export FP_PRIMS_SV=${fpPrimsSv}
      ''}
      ${pkgs.bash}/bin/bash ${pipelineScripts}/sv_mlir_to_sv.sh \
        ${circt}/bin/circt-opt ${svMlir} "$out"
    '';

  mkIlDerivation = { name, sv, slangPerFileExternModules ? false }:
    pkgs.runCommand "${name}.il" { buildInputs = [ yosysPkg ]; } ''
      ${pkgs.lib.optionalString slangPerFileExternModules ''
        export YOSYS_SLANG_PER_FILE_EXTERNS=1
      ''}
      ${pkgs.bash}/bin/bash ${pipelineScripts}/sv_to_il.sh \
        ${yosysPkg}/bin/yosys \
        ${yosysSlang}/share/yosys/plugins/slang.so \
        ${sv}/sources.f "$out"
    '';

  mkYosysStatDerivation = { name, sv, slangPerFileExternModules ? false }:
    pkgs.runCommand "${name}-yosys.stat" {
      buildInputs = [ yosysPkg python ];
    } ''
      ${pkgs.lib.optionalString slangPerFileExternModules ''
        export YOSYS_SLANG_PER_FILE_EXTERNS=1
      ''}
      ${pkgs.bash}/bin/bash ${pipelineScripts}/sv_to_yosys_stat.sh \
        ${yosysPkg}/bin/yosys \
        ${yosysSlang}/share/yosys/plugins/slang.so \
        ${sv}/sources.f "$out"
    '';

  mkDirectFailFastDerivation = { name, linalg }:
    pkgs.runCommand "${name}-direct-fail-fast" { buildInputs = [ python ]; } ''
      mkdir -p "$out"
      ${python}/bin/python ${directLowerScript} \
        --input-linalg ${linalg} \
        --out-dir "$out" \
        --mode fail-fast \
        --report-only
    '';

  mkDirectSvSmokeDerivation = { name, linalg, cutpoint ? null, fpPrimsSv ? null }:
    pkgs.runCommand "${name}-direct-sv-smoke" {
      buildInputs = [ circt python ];
    } ''
      mkdir -p "$out"
      ${pkgs.lib.optionalString (fpPrimsSv != null) ''
        export FP_PRIMS_SV=${fpPrimsSv}
      ''}
      export CIRCT_SV_EXPORT_MODE=single
      export CIRCT_STRIP_DEBUGINFO_SUFFIX=.mlir
      ${python}/bin/python ${directLowerScript} \
        --input-linalg ${linalg} \
        ${pkgs.lib.optionalString (cutpoint != null) "--cutpoint ${pkgs.lib.escapeShellArg cutpoint}"} \
        --out-dir "$out" \
        --mode sv-smoke \
        --circt-opt ${circt}/bin/circt-opt
    '';

  mkDirectSvLintDerivation = { name, directSvSmoke }:
    pkgs.runCommand "${name}-direct-sv-lint.json" {
      buildInputs = [ pkgs.verilator python ];
    } ''
      set -euo pipefail
      ${python}/bin/python - "${directSvSmoke}/direct-lower-manifest.json" "${directSvSmoke}" "$out" <<'PY'
import json
from pathlib import Path
import subprocess
import sys

manifest_path = Path(sys.argv[1])
source = sys.argv[2]
out_path = Path(sys.argv[3])
default_fail = {
    "status": "SKIP",
    "source": source,
}

if not manifest_path.exists():
    default_fail["reason"] = "direct_sv_smoke_manifest_missing"
    out_path.write_text(json.dumps(default_fail, indent=2, sort_keys=True) + "\n", encoding="utf-8")
    raise SystemExit(0)

manifest = json.loads(manifest_path.read_text(encoding="utf-8"))
sv_input_status = manifest.get("sv_input_status", "")
if sv_input_status != "hw_lowered":
    out = default_fail | {
        "reason": "unsupported_sv_input_format",
        "sv_input_status": sv_input_status,
    }
    out_path.write_text(json.dumps(out, indent=2, sort_keys=True) + "\n", encoding="utf-8")
    raise SystemExit(0)

command = [
    "verilator",
    "--top-module",
    "main",
    "--lint-only",
    "--timing",
    "--language",
    "1800-2017",
    "--threads",
    "1",
    "-Wno-fatal",
    "-f",
    f"{source}/sources.f",
]
proc = subprocess.run(command, capture_output=True, text=True, check=False)
payload = {
    "status": "PASS" if proc.returncode == 0 else "FAIL",
    "source": source,
    "verilator_exit_code": proc.returncode,
    "verilator_stdout": proc.stdout,
    "verilator_stderr": proc.stderr,
}
out_path.write_text(json.dumps(payload, indent=2, sort_keys=True) + "\n", encoding="utf-8")
if proc.returncode != 0:
    print(proc.stderr, file=sys.stderr)
    raise SystemExit(1)
PY
    '';

  mkDirectFunctionalDerivation = { name, linalg, cutpoint ? null, fixtureJson ? null }:
    pkgs.runCommand "${name}-direct-functional" {
      buildInputs = [ circt python ];
    } ''
      mkdir -p "$out"
      ${pkgs.lib.optionalString (cutpoint != null) ''
        export DIRECT_CUTPOINT=${pkgs.lib.escapeShellArg cutpoint}
      ''}
      ${pkgs.lib.optionalString (fixtureJson != null) ''
        cp "${fixtureJson}" "$out"/fixture.json
      ''}
      ${python}/bin/python ${directLowerScript} \
        --input-linalg ${linalg} \
        --out-dir "$out" \
        --mode functional \
        --circt-opt ${circt}/bin/circt-opt \
        ${pkgs.lib.optionalString (cutpoint != null) "--cutpoint \"$DIRECT_CUTPOINT\""} \
        ${pkgs.lib.optionalString (fixtureJson != null) "--fixture-json \"$out/fixture.json\""}
    '';

  mkBasePipeline = { name, torchMlirInput, handshakeFromCf
    , allowHwExterns ? false, fpPrimsSv ? null
    , slangPerFileExternModules ? false }:
    let
      self = {
        torch = mkTorchDerivation { inherit name torchMlirInput; };
        "torch-stats" = mkMlirOpStatsDerivation {
          inherit name;
          stageName = "torch";
          tool = torchMlirOpt;
          input = self.torch;
        };
        linalg = mkLinalgDerivation {
          inherit name;
          inherit (self) torch;
        };
        cf = mkCfDerivation {
          inherit name;
          inherit (self) linalg;
        };
        "cf-stats" = mkMlirOpStatsDerivation {
          inherit name;
          stageName = "cf";
          tool = "${mlir}/bin/mlir-opt";
          input = self.cf;
        };
        handshake = handshakeFromCf {
          inherit name;
          inherit (self) cf;
        };
        "hs-ext" = mkHsExtDerivation {
          inherit name;
          inherit (self) handshake;
        };
        hw0 = mkHw0Derivation {
          inherit name;
          hsExt = self."hs-ext";
        };
        hw = mkHwDerivation {
          inherit name;
          inherit (self) hw0;
        };
        "hw-clean" = mkHwCleanDerivation {
          inherit name;
          inherit (self) hw;
        };
        "sv-mlir" = mkSvMlirDerivation {
          inherit name;
          hwClean = self."hw-clean";
        };
        sv = mkSvDerivation {
          inherit name;
          svMlir = self."sv-mlir";
          inherit allowHwExterns fpPrimsSv;
        };
        "direct-fail-fast" = mkDirectFailFastDerivation {
          inherit name;
          inherit (self) linalg;
        };
        "direct-sv-smoke" = mkDirectSvSmokeDerivation {
          inherit name;
          inherit (self) linalg;
          inherit fpPrimsSv;
        };
        "direct-sv-smoke-hw-clean" = mkDirectSvSmokeDerivation {
          name = name;
          linalg = self.linalg;
          cutpoint = self."hw-clean";
          inherit fpPrimsSv;
        };
        "direct-sv-lint" = mkDirectSvLintDerivation {
          inherit name;
          directSvSmoke = self."direct-sv-smoke";
        };
        "direct-sv-lint-hw-clean" = mkDirectSvLintDerivation {
          name = name;
          directSvSmoke = self."direct-sv-smoke-hw-clean";
        };
        "direct-functional" = mkDirectFunctionalDerivation {
          inherit name;
          inherit (self) linalg;
        };
        il = mkIlDerivation {
          inherit name;
          inherit (self) sv;
          inherit slangPerFileExternModules;
        };
        "yosys-stat" = mkYosysStatDerivation {
          inherit name;
          inherit (self) sv;
          inherit slangPerFileExternModules;
        };
      };
    in self;

  mkPipeline = { name, torchMlirInput, allowHwExterns ? false, fpPrimsSv ? null
    , slangPerFileExternModules ? false }:
    mkBasePipeline {
      inherit name torchMlirInput allowHwExterns fpPrimsSv
        slangPerFileExternModules;
      handshakeFromCf = mkHandshakeDerivation;
    };

  mkLsqPipeline = { name, torchMlirInput, allowHwExterns ? false
    , fpPrimsSv ? null, slangPerFileExternModules ? false }:
    mkBasePipeline {
      inherit name torchMlirInput allowHwExterns fpPrimsSv
        slangPerFileExternModules;
      handshakeFromCf = mkHandshakeLsqDerivation;
    };

  publicStageNamesForModel = modelKey:
    if modelKey == "tiny-stories-1m" then
      builtins.filter (stage: stage != "yosys-stat") stageNames
    else
      stageNames;

  stagePathsForPipeline = publicStageNames: pipeline:
    builtins.listToAttrs (map (stage: {
      name = stage;
      value = "${builtins.getAttr stage pipeline}";
    }) publicStageNames);

  mkPipelineStagePackages = publicStageNames: name: pipeline:
    builtins.listToAttrs (map (stage: {
      name = "${name}-${stage}";
      value = builtins.getAttr stage pipeline;
    }) publicStageNames);

  mkModelMetadata = modelKey: model:
    let publicStageNames = publicStageNamesForModel modelKey;
    in pkgs.writeText "${modelKey}-pipeline-metadata.json" (builtins.toJSON {
      model = {
        inherit modelKey;
        inherit (model) name description source;
      };
      artifacts = stagePathsForPipeline publicStageNames model.pipeline;
    });

  registerPipelineModel = { pipelineFactory, name, key ? name, description ? ""
    , source ? { type = "local"; }, torchMlirInput ? null
    , torchInputCommand ? null, torchInputBuildInputs ? [ ]
    , allowHwExterns ? false, fpPrimsSv ? null
    , slangPerFileExternModules ? false }:
    let
      resolvedTorchInput = mkTorchInput {
        inherit name torchMlirInput torchInputCommand torchInputBuildInputs;
      };
      normalizedSource =
        if source ? type then source else source // { type = "local"; };
      pipeline = pipelineFactory {
        inherit name;
        torchMlirInput = resolvedTorchInput;
        inherit allowHwExterns fpPrimsSv slangPerFileExternModules;
      };
      model = {
        inherit key name description;
        source = normalizedSource;
        torchInput = resolvedTorchInput;
        inherit pipeline;
      };
      metadata = mkModelMetadata key model;
    in model // { inherit metadata; };

  registerModel = args:
    registerPipelineModel (args // { pipelineFactory = mkPipeline; });

  registerLsqModel = args:
    registerPipelineModel (args // { pipelineFactory = mkLsqPipeline; });

  registerQuantizedModel = args:
    registerPipelineModel (args // { pipelineFactory = mkLsqPipeline; });

  modelPipelinesFromRegistry = registry:
    pkgs.lib.mapAttrs (_: model: model.pipeline) registry;

  pipelineStagePackagesFromRegistry = registry:
    pkgs.lib.concatMapAttrs (name: model:
      mkPipelineStagePackages (publicStageNamesForModel name) name
      model.pipeline) registry;

  metadataPackagesFromRegistry = registry:
    pkgs.lib.mapAttrs' (name: model:
      pkgs.lib.nameValuePair "${name}-pipeline-metadata" model.metadata)
    registry;

  registryIndexPackage = registry:
    pkgs.writeText "model-registry.json" (builtins.toJSON (pkgs.lib.mapAttrs
      (name: model:
        let publicStageNames = publicStageNamesForModel name;
        in {
          inherit (model) name description source;
          packages = builtins.listToAttrs (map (stage: {
            name = stage;
            value = "${name}-${stage}";
          }) publicStageNames);
        }) registry));
in {
  inherit registerModel;
  inherit registerLsqModel;
  inherit registerQuantizedModel;
  inherit modelPipelinesFromRegistry;
  inherit pipelineStagePackagesFromRegistry;
  inherit metadataPackagesFromRegistry;
  inherit registryIndexPackage;
}
